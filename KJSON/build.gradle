plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.10'
    id 'org.jetbrains.dokka' version '2.0.0'
    id 'maven-publish'
}

group = 'pt.iscte'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
}

test {
    useJUnitPlatform()
}

kotlin {
    jvmToolchain(23)
}

sourceSets {
    main {
        kotlin {
            srcDirs = ['src/main']
        }
    }
    test {
        kotlin {
            srcDirs = ['src/test']
        }
    }
}

dokka {
    moduleName.set("KJSON")
    dokkaPublications.html {
        suppressInheritedMembers.set(true)
        failOnWarning.set(true)
    }
    dokkaSourceSets.main {
//        includes.from("README.md")
        sourceLink {
            localDirectory.set(file("src/main"))
            remoteUrl("http://localhost:63342/docs")
            remoteLineSuffix.set("#L")
        }
    }
    pluginsConfiguration.html {
        footerMessage.set("Â© Alexandre Cortez & Pedro Moura")
    }
}

// Define the tasks directly without registering them first
task dokkaHtmlJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = 'Assembles a jar archive containing the HTML documentation.'
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = 'Assembles a jar archive containing the source code.'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

// Configure the publishing in afterEvaluate to ensure tasks are fully configured
afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                // Add the artifacts explicitly
                artifact sourcesJar
                artifact dokkaHtmlJar

                pom {
                    name = 'KJSON'
                    description = 'A Kotlin JSON library'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'AlexandreMilharado'
                            name = 'Alexandre Cortez'
                        }
                        developer {
                            id = 'pafmoura'
                            name = 'Pedro Moura'
                        }
                    }
                }
            }
        }
    }
}